# Advent of Code (AoC) - day 16, task 2

import "prelude.eg"
import "os.ego"
import "regex.ego"
import "search.eg"
import "dictionary.eg"
using System
using OS
using List
using Search

def u = unpack

def input = let L = read_line stdin in if eof stdin then nil else cons L input
val digit = Regex::compile "[0-9|A-F]*"
def parse_line = flatten . map u . Regex::matches digit   

val hexit = (dict_from_list . map [(A,B) -> (A, u B)])
      { ('0', "0000"), ('1', "0001"), ('2', "0010"), ('3', "0011"),
        ('4', "0100"), ('5', "0101"), ('6', "0110"), ('7', "0111"),
        ('8', "1000"), ('9', "1001"), ('A', "1010"), ('B', "1011"),
        ('C', "1100"), ('D', "1101"), ('E', "1110"), ('F', "1111") }

def unhex = flatten . map (dict_get hexit)
def bits = foldl [N '0' -> 2*N | N '1' -> 1+2*N] 0

def look    = [ {} -> fail {} | {B|BB} -> success B BB ]
def lookn   = [ 0  -> success {} | N -> look <*> \B -> (lookn (N - 1)) <@> \BB -> {B|BB} ]
def satisfy = [ BB0 -> (lookn (length BB0)) <*> \BB1 -> if BB0 == BB1 then success BB0 else fail ]

data op, lit

def literal =  (satisfy (u"0") <*> \_ -> lookn 4)
           <+> ((satisfy (u"1")) <*> \_ -> (lookn 4) <*> \BB0 -> literal <@> \BB1 -> BB0++BB1)
def operator = (satisfy (u"0") <*> \_ -> lookn 15 <*> \BB -> lookn (bits BB) <@> \BB -> (parse_packets BB))
           <+> (satisfy (u"1") <*> \_ -> lookn 11 <*> \BB -> packetsn (bits BB))
def packet = (lookn 3) <*> \VERSION -> 
    ((satisfy (u"100")) <*> \ID -> literal <@> \BB -> lit (bits VERSION) (bits ID) (bits BB)) <+> 
    ((lookn 3) <*> \ID -> operator <@> \II -> op (bits VERSION) (bits ID) II)
def packetsn = [ 0 -> success {} | N -> packet <*> \P -> packetsn (N - 1) <@> \PP -> {P|PP} ] 

def parse_packet   = [ BB -> search packet        BB [X BB -> X] [X BB -> throw X] [X BB -> throw X] ]
def parse_packets  = [ BB -> search (plus packet) BB [X BB -> X] [X BB -> throw X] [X BB -> throw X] ]

def eval = [ (op _ 0 PP) -> sum (map eval PP)
           | (op _ 1 PP) -> product (map eval PP)
           | (op _ 2 PP) -> minimum (map eval PP)
           | (op _ 3 PP) -> maximum (map eval PP)
           | (op _ 5 {P0,P1}) -> if (eval P1) < (eval P0) then 1 else 0
           | (op _ 6 {P0,P1}) -> if (eval P0) < (eval P1) then 1 else 0
           | (op _ 7 {P0,P1}) -> if (eval P0) == (eval P1) then 1 else 0 
           | (lit _ _ N) -> N ]

def main =
    let P = (unhex . parse_line) (head input) in
    let T = parse_packet P in eval T

